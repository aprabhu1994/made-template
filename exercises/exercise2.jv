pipeline TrainstopsDBPipeline {
    TrainstopsDBCSVExtractor
        -> StatusColumnDeleter
        -> TrainstopsDBTableInterpreter
        -> TrainstopsDBLoader;

    block TrainstopsDBCSVExtractor oftype CSVExtractor {
        url: "https://download-data.deutschebahn.com/static/datasets/haltestellen/D_Bahnhof_2020_alle.CSV";
        delimiter: ";";
    }

    block StatusColumnDeleter oftype ColumnDeleter {
        delete: [column J];
    }

    block TrainstopsDBTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "EVA_NR" oftype integer,
            "DS100" oftype TextType,
            "IFOPT" oftype IfOptType,
            "NAME" oftype TextType,
            "Verkehr" oftype VerkehrType,
            "Laenge" oftype CordinateType,
            "Breite" oftype CordinateType,
            "Betreiber_Name" oftype TextType,
            "Betreiber_Nr" oftype integer,
        ];
    }

    block TrainstopsDBLoader oftype SQLiteLoader {
        table: "trainstops";
        file: "./trainstops.sqlite";
    }

    valuetype VerkehrType oftype text {
        constraints: [ ValidVerkehrValues ];
    }

    valuetype CordinateType oftype decimal {
        constraints: [ ValidGeographicCordinates ];
    }

    valuetype IfOptType oftype text {
        constraints: [ ValidIfOptValues ];
    }

    valuetype TextType oftype text {
        constraints: [ NonEmptyValues ];
    }

    constraint ValidVerkehrValues oftype AllowlistConstraint {
        allowlist: ["FV", "RV", "nur DPN"];
    }

    constraint ValidGeographicCordinates oftype RangeConstraint {
        lowerBound: -90;
        upperBound: 90;
    }

    constraint ValidIfOptValues oftype RegexConstraint {
        regex: /^[a-zA-Z]{2}:\d+:\d+(?::\d+)?$/;
    }

    constraint NonEmptyValues oftype LengthConstraint {
        minLength: 1;
        maxLength: 9007199254740991;
    }
}
